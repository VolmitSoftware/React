plugins {
    id 'java'
    id 'idea'
    id 'java-library'
    id 'io.freefair.lombok' version '5.2.1'
    id "com.github.johnrengelman.shadow" version "6.1.0"
}

group 'com.volmit.react'
version '6.655'
def name = 'React'


// ADD YOURSELF AS A NEW LINE IF YOU WANT YOUR OWN BUILD TASK GENERATED
// ==============================================================
registerCustomOutputTask('Cyberpwn', 'C://Users/cyberpwn/Documents/development/server/plugins');
registerCustomOutputTask('ArcaneArts', 'C://Users/arcane/Documents/development/server/plugins');
// ==============================================================

def registerCustomOutputTask(name, path) {
    tasks.register('build' + name, Copy) {
        group('development')
        outputs.upToDateWhen{false}
        dependsOn ':shadowJar'
        from(file('build/libs/React-' + version + '-all.jar'))
        into(file(path))
        rename { String fileName ->
            fileName.replace('React-' + version + '-all.jar', "React.jar")
        }
    }
}

repositories {
    maven {
        allowInsecureProtocol true
        url "http://archive.arcane.art/repository/arcane"
    }
    mavenLocal()
}

shadowJar
{
    minimize()
    dependencies {
        include(dependency('org.zeroturnaround:zt-zip:1.14'))
        include(dependency('com.github.cryptomorin:XSeries:7.6.1'))
    }
}

processResources {
    filesMatching('**/plugin.yml') {
        expand(
            'name': name.toString(),
            'version': version.toString()
        )
    }
}

idea {
    module {
        downloadJavadoc = false
        downloadSources = false
    }
}

dependencies {
    implementation 'org.zeroturnaround:zt-zip:1.14'
    implementation 'com.github.cryptomorin:XSeries:7.6.1'
    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
    implementation 'org.spigotmc.full:1.12.2:1.12.2'
    implementation 'org.bukkit.craftbukkit:1.17:1.17'
    implementation 'org.bukkit.craftbukkit:1.16.5:1.16.5'
    implementation 'org.bukkit.craftbukkit:1.16.4:1.16.4'
    implementation 'org.bukkit.craftbukkit:1.16.3:1.16.3'
    implementation 'org.bukkit.craftbukkit:1.16.2:1.16.2'
    implementation 'org.bukkit.craftbukkit:1.16.1:1.16.1'
    implementation 'org.bukkit.craftbukkit:1.15.1:1.15.1'
    implementation 'org.bukkit.craftbukkit:1.14.4:1.14.4'
    implementation 'org.bukkit.craftbukkit:1.13.2:1.13.2'
    implementation 'org.bukkit.craftbukkit:1.12.2:1.12.2'
    implementation 'org.bukkit.craftbukkit:1.11.2:1.11.2'
    implementation 'org.bukkit.craftbukkit:1.10.2:1.10.2'
    implementation 'org.bukkit.craftbukkit:1.9.4:1.9.4'
    implementation 'org.bukkit.craftbukkit:1.8.8:1.8.8'
    implementation 'com.comphenix.protocol:ProtocolLib:4.5.1'
    implementation 'io.lumine.xikage:MythicMobs:4.9.1'
    implementation 'me.clip:placeholderapi:2.9.2'
    implementation 'com.boydti:fawe-api:latest'
}





