/*
 * React is Copyright (c) 2021 Arcane Arts (Volmit Software)
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

plugins {
    id 'java'
    id 'java-library'
    id "com.gradleup.shadow" version "8.3.5"
}


version '1.0.3' // Needs to be version specific 
def apiVersion = '1.19'
def name = getRootProject().getName() // Defined in settings.gradle
def main = 'com.volmit.react.React'

// ADD YOURSELF AS A NEW LINE IF YOU WANT YOUR OWN BUILD TASK GENERATED
// ======================== WINDOWS =============================
registerCustomOutputTask('Cyberpwn', 'C://Users/cyberpwn/Documents/development/server/plugins')
registerCustomOutputTask('Psycho', 'C://Dan/MinecraftDevelopment/server/plugins')
registerCustomOutputTask('ArcaneArts', 'C://Users/arcane/Documents/development/server/plugins')
registerCustomOutputTask('Vatuu', 'D://Minecraft/Servers/1.19/plugins')
registerCustomOutputTask('Nowhere', 'E://Desktop/server/plugins')
registerCustomOutputTask('Pixel', 'C://Users//repix//Iris Dimension Engine//1.20.4 - Development/plugins')
registerCustomOutputTask('CrazyDev22', 'C:\\Users\\Julian\\Desktop\\server\\plugins')
// ========================== UNIX ==============================
registerCustomOutputTaskUnix('CyberpwnLT', '/Users/danielmills/development/server/plugins')
registerCustomOutputTaskUnix('PsychoLT', '/Users/brianfopiano/Desktop/REMOTES/RemoteMinecraft/plugins')
// ==============================================================

/**
 * Gradle is weird sometimes, we need to delete the plugin yml from the build folder to actually filter properly.
 */
file(jar.archiveFile.get().getAsFile().getParentFile().getParentFile().getParentFile().getAbsolutePath() + '/build/resources/main/plugin.yml').delete()

/**
 * Expand properties into plugin yml
 */

processResources {
    filesMatching('**/plugin.yml') {
        expand(
                'name': name.toString(),
                'version': version.toString(),
                'main': main.toString(),
                'apiversion': apiVersion.toString()
        )
    }
}

repositories {
    mavenCentral()
    maven { url 'https://www.jitpack.io' }
    maven { url "https://repo.papermc.io/repository/maven-public/"}
    maven { url "https://mvn.lumine.io/repository/maven-public/" }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

/**
 * We need parameter meta for the decree command system
 */
compileJava {
    options.compilerArgs << '-parameters'
}

/**
 * Configure React for shading
 */
shadowJar {
    minimize()
    append("plugin.yml")
    dependencies {
        include(dependency('com.github.VolmitDev:'))
    }

    archiveClassifier = null
    relocate("art.arcane", "com.volmit.react.util.arcane")
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 60, 'minutes'
    resolutionStrategy.cacheDynamicVersionsFor 60, 'minutes'
}


dependencies {
    // Provided or Classpath
    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    compileOnly 'org.spigotmc:spigot-api:1.19.4-R0.1-SNAPSHOT'
    compileOnly "io.papermc:paperlib:1.0.7"

    // Shaded
    implementation 'com.github.VolmitDev:Curse:23.4.3'
    implementation 'com.github.VolmitDev:MultiBurst:22.9.2'
    implementation 'com.github.VolmitDev:Chrono:22.9.10'
    implementation 'com.github.VolmitDev:Spatial:22.11.1'

    //Random API's
    compileOnly 'me.clip:placeholderapi:2.11.6'


    // Dynamically Loaded
    compileOnly 'net.kyori:adventure-text-minimessage:4.24.0'
    compileOnly 'net.kyori:adventure-platform-bukkit:4.4.1'
    compileOnly 'net.kyori:adventure-api:4.9.3'
    compileOnly 'com.github.oshi:oshi-core:5.8.5'
    compileOnly 'io.timeandspace:smoothie-map:2.0.2'
    compileOnly 'it.unimi.dsi:fastutil:8.5.8'
    compileOnly 'com.googlecode.concurrentlinkedhashmap:concurrentlinkedhashmap-lru:1.4.2'
    compileOnly 'org.apache.commons:commons-lang3:3.12.0'
    compileOnly 'com.google.code.gson:gson:2.10'
    compileOnly 'org.zeroturnaround:zt-zip:1.15'
    compileOnly 'org.bitbucket.mstrobel:procyon-core:0.6.0'
    compileOnly 'org.bitbucket.mstrobel:procyon-compilertools:0.6.0'
    compileOnly 'org.bitbucket.mstrobel:procyon-reflection:0.6.0'
    compileOnly 'fr.inria.gforge.spoon:spoon-core:10.2.0'
    compileOnly 'com.github.ben-manes.caffeine:caffeine:3.0.6'
    compileOnly 'org.java-websocket:Java-WebSocket:1.5.3'
    compileOnly 'io.netty:netty-transport:4.1.92.Final'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

compileJava {
    options.encoding = "UTF-8"
}

def buildFile = layout.buildDirectory.file("libs/React-${version}.jar")

def registerCustomOutputTask(name, path) {
    if (!System.properties['os.name'].toLowerCase().contains('windows')) {
        return
    }

    tasks.register('build' + name, Copy) {
        group('development')
        outputs.upToDateWhen { false }
        dependsOn(shadowJar)
        from(buildFile)
        into(file(path))
        rename { String fileName ->
            fileName.replace("React-" + version + ".jar", "React.jar")
        }
    }
}

def registerCustomOutputTaskUnix(name, path) {
    if (System.properties['os.name'].toLowerCase().contains('windows')) {
        return
    }

    tasks.register('build' + name, Copy) {
        group('development')
        outputs.upToDateWhen { false }
        dependsOn(shadowJar)
        from(buildFile)
        into(file(path))
        rename { String fileName ->
            fileName.replace("React-" + version + ".jar", "React.jar")
        }
    }
}

